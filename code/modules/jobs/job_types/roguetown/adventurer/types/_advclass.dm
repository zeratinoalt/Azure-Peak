/datum/advclass
	var/name
	var/list/classes
	var/outfit
	var/tutorial = "Choose me!"
	var/townie_contract_gate_exempt = FALSE
	var/townie_contract_gate_hide_in_list = FALSE
	/// Subclass-specific tutorial shown via to_chat on spawn, separate from the class-picker tutorial.
	var/subclass_tutorial
	var/list/allowed_sexes
	var/list/forbidden_races
	var/list/allowed_patrons
	var/list/allowed_ages
	var/pickprob = 100
	var/maximum_possible_slots = -1
	var/total_slots_occupied = 0
	var/min_pq = -100
	var/class_select_category

	var/horse = FALSE
	var/vampcompat = TRUE
	var/list/traits_applied
	var/cmode_music

	var/noble_income = FALSE //Passive income every day from noble estate

	/// This class is immune to species-based swapped gender locks
	var/immune_to_genderswap = FALSE

	//What categories we are going to sort it in
	var/list/category_tags = list(CTAG_DISABLED)

	/// Whether this class will apply the adaptive name to the job it belongs to.
	var/adaptive_name = FALSE

	/// Stat ceilings for the specific subclass.
	var/list/adv_stat_ceiling

	/// Subclass stat bonuses.
	var/list/subclass_stats

	/// Subclass skills. Everything here is leveled UP TO using adjust_skillrank_up_to EX. list(/datum/skill = SKILL_LEVEL_JOURNEYMAN)
	var/list/subclass_skills

	/// Subclass languages.
	var/list/subclass_languages

	/// Subclass virtues.
	var/list/subclass_virtues

	/// Mage aspect system config. If set, opens the Grimoire on learnspell.
	/// Keys: "mastery" (bool), "major" (int), "minor" (int), "utilities" (int)
	var/list/subclass_mage_aspects

	/// List of items to put in an item stash
	var/list/subclass_stashed_items = list()

	/// Extra fluff added to the role explanation in class selection.
	var/extra_context

	/// Set to FALSE to skip apply_character_post_equipment() which applies virtue, flaw, loadout
	var/applies_post_equipment = TRUE

	/// set to TRUE to reset stats in equipme, clearing any racial bonuses or bonuses the character had before becoming this class
	var/reset_stats = FALSE

	var/list/virtue_limits = list()
	var/list/vice_limits = list()

	var/datum/class_age_mod/age_mod = null

	var/class_tempo_faction = null

	var/tempo_capable = TRUE

/datum/advclass/New()
	if(ispath(age_mod) && !istype(age_mod))
		var/datum/class_age_mod/newmod = new age_mod()
		age_mod = newmod
	. = ..()

/mob/living/carbon/human/proc/get_advclass_datum()
	RETURN_TYPE(/datum/advclass)
	if(mind?.picked_advclass)
		return mind.picked_advclass
	if(advjob)
		return SSrole_class_handler.get_advclass_by_name(advjob)

/datum/advclass/proc/equipme(mob/living/carbon/human/H, dummy = FALSE)
	// input sleeps....
	set waitfor = FALSE
	if(!H)
		return FALSE

	if(outfit)
		H.equipOutfit(outfit, dummy)

		if(dummy)	//This means we're doing a Char Sheet preview. We don't need to equip the dummy with anything else, the outfits are likely to runtime on their own.
			return

	post_equip(H)

	H.flag_gear_as_worn()

	H.advjob = name

	var/turf/TU = get_turf(H)
	if(TU)
		if(horse)
			var/mob/horse_mob = new horse(TU)
			if(istype(horse_mob, /mob/living/simple_animal/hostile/retaliate/rogue))
				var/mob/living/simple_animal/hostile/retaliate/rogue/rogue_animal = horse_mob
				rogue_animal.owner = H
				rogue_animal.friends |= H

	for(var/trait in traits_applied)
		ADD_TRAIT(H, trait, ADVENTURER_TRAIT)

	if(noble_income)
		var/already_has_income = !isnull(SStreasury.noble_incomes[H])
		SStreasury.noble_incomes[H] = noble_income
		SStreasury.grant_estate_income(H, noble_income, !already_has_income)

	if(adaptive_name)
		H.adaptive_name = TRUE

	if(length(subclass_languages))
		for(var/lang in subclass_languages)
			H.grant_language(lang)

	if(reset_stats)
		H.reset_stats()

	if(length(subclass_stats))
		for(var/stat in subclass_stats)
			H.change_stat(stat, subclass_stats[stat])

	if(length(subclass_skills))
		for(var/skill in subclass_skills)
			H.adjust_skillrank_up_to(skill, subclass_skills[skill], TRUE)

	// Set up spell systems before virtues so Arcyne Potential can detect and add to them
	if(LAZYLEN(subclass_mage_aspects))
		H.mind?.setup_mage_aspects(subclass_mage_aspects.Copy())

	if(length(subclass_virtues))
		for(var/virtue in subclass_virtues)
			apply_virtue(H, new virtue)

	if(age_mod)
		if(istype(age_mod))
			age_mod.apply_age_mod(H)

	if(length(subclass_stashed_items))
		if(!H.mind)
			return
		for(var/stashed_item in subclass_stashed_items)
			H.mind?.special_items[stashed_item] = subclass_stashed_items[stashed_item]

	// After the end of adv class equipping, apply a SPECIAL trait if able

	if(applies_post_equipment)
		apply_character_post_equipment(H)
	H.set_advsetup(FALSE)
	H.mind?.refresh_spell_buttons()
//======== Massive shitcode, that works at least.
/datum/advclass/proc/get_vice_limits(mob/living/carbon/human/H)
	if(length(vice_limits))
		return vice_limits.Copy()
	return list()

/datum/advclass/proc/get_prefs_vice_limits(client/player)
	if(length(vice_limits))
		return vice_limits.Copy()
	return list()

/datum/advclass/proc/is_vice_limited(vice, list/limited_vices)
	if(isnull(limited_vices))
		limited_vices = vice_limits
	if(!length(limited_vices) || !vice)
		return FALSE
	for(var/vicetype in limited_vices)
		if(ispath(vice, vicetype) || istype(vice, vicetype))
			return TRUE
	return FALSE

/datum/advclass/proc/has_limited_vice(list/current_vices, list/limited_vices)
	if(isnull(limited_vices))
		limited_vices = vice_limits
	if(!length(current_vices) || !length(limited_vices))
		return FALSE
	for(var/vice in current_vices)
		if(is_vice_limited(vice, limited_vices))
			return TRUE
	return FALSE

/datum/advclass/proc/get_limited_vice_names(list/current_vices, list/limited_vices)
	. = list()
	if(isnull(limited_vices))
		limited_vices = vice_limits
	if(!length(current_vices) || !length(limited_vices))
		return
	for(var/cf_type in current_vices)
		if(is_vice_limited(cf_type, limited_vices))
			var/datum/charflaw/cf = GLOB.character_flaws_singletons[cf_type]
			. += cf.name

/datum/advclass/proc/get_prefs_restriction_names(client/player)
	. = list()
	if(!player?.prefs)
		return
	if(length(virtue_limits))
		for(var/virtuetype in virtue_limits)
			if(istype(player.prefs.virtue, virtuetype))
				. += player.prefs.virtue.name
			if(istype(player.prefs.virtuetwo, virtuetype))
				. += player.prefs.virtuetwo.name
	. += get_limited_vice_names(player.prefs.charflaws, get_prefs_vice_limits(player))
//===
/datum/advclass/proc/post_equip(mob/living/carbon/human/H)
	if(H.ckey)
		SScrediticons.processing[H.ckey] = TRUE
	if(cmode_music)
		H.cmode_music = cmode_music
	if(class_tempo_faction)
		H.tempo_faction_flag = class_tempo_faction

/*
	Whoa! we are checking requirements here!
	On the datum! Wow!
*/
/datum/advclass/proc/check_requirements(mob/living/carbon/human/H)

	var/datum/species/pref_species = H.dna.species
	var/list/local_allowed_sexes = list()
	if(length(allowed_sexes))
		local_allowed_sexes |= allowed_sexes
	if(!immune_to_genderswap && pref_species?.gender_swapping)
		if(MALE in allowed_sexes)
			local_allowed_sexes -= MALE
			local_allowed_sexes += FEMALE
		if(FEMALE in allowed_sexes)
			local_allowed_sexes -= FEMALE
			local_allowed_sexes += MALE
	if(length(local_allowed_sexes) && !(H.gender in local_allowed_sexes))
		return FALSE

	if(length(forbidden_races) && (H.dna.species.type in forbidden_races))
		return FALSE

	if(length(allowed_ages) && !(H.age in allowed_ages))
		return FALSE

	if(length(allowed_patrons) && !(H.patron.type in allowed_patrons))
		return FALSE

	if(length(virtue_limits) && H.client)
		for(var/virtuetype in virtue_limits)
			if(istype(H.client.prefs?.virtue, virtuetype) || istype(H.client.prefs?.virtuetwo, virtuetype))
				return FALSE

	var/list/current_vice_limits = get_vice_limits(H)
	if(length(current_vice_limits) && H.client)
		if(has_limited_vice(H.charflaws, current_vice_limits))
			return FALSE

	if(maximum_possible_slots > -1)
		if(total_slots_occupied >= maximum_possible_slots)
			return FALSE

	#ifdef USES_PQ
	if(min_pq != -100) // If someone sets this we actually do the check.
		if(!(get_playerquality(H.client.ckey) >= min_pq))
			return FALSE
	#endif

	if(prob(pickprob))
		return TRUE

/datum/advclass/proc/prefs_lock_reason(datum/preferences/prefs)
	if(!prefs)
		return "unavailable"

	var/datum/species/pref_species = prefs.pref_species
	if(length(allowed_sexes))
		var/list/local_allowed_sexes = allowed_sexes.Copy()
		if(!immune_to_genderswap && pref_species?.gender_swapping)
			if(MALE in allowed_sexes)
				local_allowed_sexes -= MALE
				local_allowed_sexes += FEMALE
			if(FEMALE in allowed_sexes)
				local_allowed_sexes -= FEMALE
				local_allowed_sexes += MALE
		if(length(local_allowed_sexes) && !(prefs.gender in local_allowed_sexes))
			return "sex"

	if(length(forbidden_races) && (pref_species?.type in forbidden_races))
		return "species"

	if(length(allowed_ages) && !(prefs.age in allowed_ages))
		return "age"

	if(length(allowed_patrons) && !(prefs.selected_patron?.type in allowed_patrons))
		return "faith"

	if(length(virtue_limits))
		for(var/virtuetype in virtue_limits)
			if(istype(prefs.virtue, virtuetype) || istype(prefs.virtuetwo, virtuetype))
				return "virtue"

	#ifdef USES_PQ
	if(min_pq != -100 && prefs.parent)
		if(get_playerquality(prefs.parent.ckey) < min_pq)
			return "reputation"
	#endif

	return null

// Basically the handler has a chance to plus up a class, heres a generic proc you can override to handle behavior related to it.
// For now you just get an extra stat in everything depending on how many plusses you managed to get.
/datum/advclass/proc/boost_by_plus_power(plus_factor, mob/living/carbon/human/H)
	for(var/S in MOBSTATS)
		H.change_stat(S, plus_factor)


//Final proc in the set for really silly shit
///datum/advclass/proc/extra_slop_proc_ending(mob/living/carbon/human/H)
