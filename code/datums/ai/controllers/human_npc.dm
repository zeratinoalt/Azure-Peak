/// Base human NPC controller. Holds infrastructure shared by all humanoid NPC archetypes:
/// movement settings, targeting, common subtrees, and signal wiring. Do not assign this
/// controller directly — use /melee or /archer subtypes.
/datum/ai_controller/human_npc
	max_target_distance = 13
	ai_movement = /datum/ai_movement/hybrid_pathing
	blackboard = list(
		BB_WEAPON_TYPE = /obj/item/rogueweapon,
		BB_ARMOR_CLASS = 2,
		BB_TARGETTING_DATUM = new /datum/targetting_datum/basic(),
		BB_PET_TARGETING_DATUM = new /datum/targetting_datum/basic/not_friends(),

		BB_HUMAN_NPC_SWINGS_TAKEN = 0,			// swings this engagement, gates the special opener
		BB_HUMAN_NPC_SWINGS_TARGET = null,		// who the above is counting against
		BB_HUMAN_NPC_ZONE_COMMIT_COUNTER = 0,	// swings spent on the current self-picked zone
		BB_HUMAN_NPC_LAST_ATTACK_ZONE = null,	// last zone we attacked
		BB_HUMAN_NPC_WEAKPOINT = null,			// cached weakpoint zone if we found one
		BB_HUMAN_NPC_JUMP_COOLDOWN = 0,		// world.time when we can next jump
		BB_HUMAN_NPC_HARASS_MODE = FALSE,		// TRUE when in hit-and-run mode
		BB_HUMAN_NPC_HARASS_RETREATING = FALSE,// TRUE when in the back-off phase of harass
		BB_HUMAN_NPC_HARASS_COOLDOWN = 0,		// world.time before we can dart in again
	)
	/// Subtrees shared by all human NPC archetypes. Subtypes prepend archetype-specific
	/// subtrees via their own planning_subtrees list.
	planning_subtrees = list(
		// /datum/ai_planning_subtree/pet_planning, - TEMP COMMENT OUT
		/datum/ai_planning_subtree/being_a_minion,
		/datum/ai_planning_subtree/call_for_help,
		/datum/ai_planning_subtree/generic_break_restraints,
		/datum/ai_planning_subtree/use_throwable,
		/datum/ai_planning_subtree/generic_wield,
		/datum/ai_planning_subtree/kick_attack,
		/datum/ai_planning_subtree/generic_resist,
		/datum/ai_planning_subtree/generic_stand,
		/datum/ai_planning_subtree/flee_target,
		/datum/ai_planning_subtree/tree_climb,
		/datum/ai_planning_subtree/aggro_find_target,
		/datum/ai_planning_subtree/attack_obstacle_in_path,
		/datum/ai_planning_subtree/leap_attack,
		/datum/ai_planning_subtree/basic_melee_attack_subtree/human_npc,
		/datum/ai_planning_subtree/find_weapon,
		/datum/ai_planning_subtree/equip_item,
		/datum/ai_planning_subtree/loot,
	)

/datum/ai_controller/human_npc/TryPossessPawn(atom/new_pawn)
	. = ..()
	new_pawn.AddComponent(/datum/component/ai_inventory_manager)
	new_pawn.AddElement(/datum/element/interrupt_on_damage)
	new_pawn.AddComponent(/datum/component/combat_vocalizer)

/datum/ai_controller/human_npc/UnpossessPawn(destroy)
	var/mob/living/living_pawn = pawn
	living_pawn.RemoveElement(/datum/element/interrupt_on_damage)
	qdel(living_pawn.GetComponent(/datum/component/ai_inventory_manager))
	qdel(living_pawn.GetComponent(/datum/component/combat_vocalizer))
	return ..()

/datum/ai_controller/human_npc/can_move()
	. = ..()
	if(!.)
		return FALSE
	var/mob/living/living_pawn = pawn
	if(living_pawn.pulledby)
		return FALSE

/mob/living/carbon/human/proc/upgrade_ai_controller(new_controller_type)
	if(!ispath(new_controller_type, /datum/ai_controller))
		CRASH("upgrade_ai_controller called with non-controller typepath: [new_controller_type]")
	if(istype(ai_controller, new_controller_type))
		return // already on the right controller
	if(ai_controller)
		QDEL_NULL(ai_controller)
	ai_controller = new_controller_type
	InitializeAIController()

/// Melee-only human NPC. Same as base — the base class is already melee-focused since it
/datum/ai_controller/human_npc/melee

/// Archer human NPC. Adds archer-specific planning subtrees and blackboard keys on top of
/// the base. Uses the same melee subtree as a fallback when out of ammo / in melee range.
/datum/ai_controller/human_npc/archer
	blackboard = list(
		BB_WEAPON_TYPE = /obj/item/rogueweapon,
		BB_ARMOR_CLASS = 2,
		BB_TARGETTING_DATUM = new /datum/targetting_datum/basic(),
		BB_PET_TARGETING_DATUM = new /datum/targetting_datum/basic/not_friends(),

		BB_HUMAN_NPC_SWINGS_TAKEN = 0,
		BB_HUMAN_NPC_SWINGS_TARGET = null,
		BB_HUMAN_NPC_ZONE_COMMIT_COUNTER = 0,
		BB_HUMAN_NPC_LAST_ATTACK_ZONE = null,
		BB_HUMAN_NPC_WEAKPOINT = null,
		BB_HUMAN_NPC_JUMP_COOLDOWN = 0,
		BB_HUMAN_NPC_HARASS_MODE = FALSE,
		BB_HUMAN_NPC_HARASS_RETREATING = FALSE,
		BB_HUMAN_NPC_HARASS_COOLDOWN = 0,

		// Archer-specific state — only relevant to mobs that carry a bow
		BB_ARCHER_NPC_BOW = null,
		BB_ARCHER_NPC_QUIVER = null,
		BB_ARCHER_NPC_EQUIPMENT_CACHE_EXPIRY = 0,
		BB_ARCHER_NPC_TARGET_ARROW = null,
		BB_ARCHER_NPC_STASHED_WEAPON = null,
		BB_ARCHER_NPC_NEXT_SHOT = 0,
		BB_ARCHER_NPC_REPOSITION_TURF = null,
		BB_ARCHER_NPC_REPOSITION_UNTIL = 0,
	)
	planning_subtrees = list(
		/datum/ai_planning_subtree/being_a_minion,
		/datum/ai_planning_subtree/call_for_help,
		/datum/ai_planning_subtree/generic_break_restraints,
		/datum/ai_planning_subtree/use_throwable,
		/datum/ai_planning_subtree/generic_wield,
		/datum/ai_planning_subtree/kick_attack,
		/datum/ai_planning_subtree/generic_resist,
		/datum/ai_planning_subtree/generic_stand,
		/datum/ai_planning_subtree/flee_target,
		/datum/ai_planning_subtree/tree_climb,
		/datum/ai_planning_subtree/archer_base, // Archer only
		/datum/ai_planning_subtree/retrieve_arrows, // Archer only
		/datum/ai_planning_subtree/ranged_attack_subtree, // Archer only
		/datum/ai_planning_subtree/aggro_find_target,
		/datum/ai_planning_subtree/attack_obstacle_in_path,
		/datum/ai_planning_subtree/leap_attack,
		/datum/ai_planning_subtree/basic_melee_attack_subtree/human_npc,
		/datum/ai_planning_subtree/find_weapon,
		/datum/ai_planning_subtree/equip_item,
		/datum/ai_planning_subtree/loot,
	)


/proc/npc_technique_cd(mob/living/user, base_cd)
	if(HAS_TRAIT(user, TRAIT_CONJURED_SUMMON))
		return base_cd / 1.5
	return base_cd

// Make it so that every other human NPC in view attacking the same target inherits the cooldown, so that they cannot be chained.
/proc/propagate_technique_cd(mob/living/user, atom/target, bb_key, cd_end, specialcd_duration = 0)
	if(!target)
		return
	for(var/mob/living/carbon/human/ally in view(5, user))
		if(ally == user || ally.client || ally.stat == DEAD)
			continue
		var/datum/ai_controller/AC = ally.ai_controller
		if(!istype(AC, /datum/ai_controller/human_npc))
			continue
		if(AC.blackboard[BB_BASIC_MOB_CURRENT_TARGET] != target)
			continue
		if(bb_key)
			AC.set_blackboard_key(bb_key, max(AC.blackboard[bb_key] || 0, cd_end))
		if(specialcd_duration)
			ally.apply_status_effect(/datum/status_effect/debuff/specialcd, specialcd_duration)
