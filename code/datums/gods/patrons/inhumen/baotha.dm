/datum/patron/inhumen/baotha
	name = "Baotha"
	domain = "Goddess of Comfort, Passion, Addiction, and Heartbreak"
	desc = "Belladoth was the Eleventh of the Pantheon, In taboo rituo, she took on the pain of the uncomforted and outcast, those that had been rejected by her siblings; and together, their pain became Baotha. A saccharine truth that the hurts of the world need not be shouldered alone, no matter who you are. She offers succor to those that cannot find it elsewhere. Baothans range from the Heartbroken and Damaged to those that have simply turned to Nihilism in the face of the death of Psydonia."
	worshippers = "The Anguished, the Hollow, the Heartbroken, the Addicted, those who break taboo"
	mob_traits = list(TRAIT_DEPRAVED, TRAIT_CICERONE, TRAIT_BAOTHAN_CALM) ///this is fine
	miracles = list(/datum/action/cooldown/spell/touch/orison						= CLERIC_ORI,
					/datum/action/cooldown/spell/baotha/emotional_sway			= CLERIC_T0,
					/obj/effect/proc_holder/spell/invoked/baothavice				= CLERIC_T0,
					/obj/effect/proc_holder/spell/invoked/baothablessings			= CLERIC_T0,
					/datum/action/cooldown/spell/miracle/heal						= CLERIC_T1,
					/datum/action/cooldown/spell/miracle/bloodmiracle				= CLERIC_T1,
					/obj/effect/proc_holder/spell/invoked/heart_on_sleeve			= CLERIC_T1,
					/obj/effect/proc_holder/spell/invoked/griefflower				= CLERIC_T1,
					/obj/effect/proc_holder/spell/invoked/projectile/blowingdust	= CLERIC_T2,
					/obj/effect/proc_holder/spell/invoked/lasthigh					= CLERIC_T2,
					/obj/effect/proc_holder/spell/invoked/joyride					= CLERIC_T3,
					/obj/effect/proc_holder/spell/invoked/painkiller				= CLERIC_T3,
					/obj/effect/proc_holder/spell/invoked/resurrect/baotha			= CLERIC_T4,
	)
	confess_lines = list(
		"BAOTHA DEMANDS PLEASURE!",
		"LIVE, LAUGH, LOVE!",
		"BAOTHA IS MY JOY!",
	)
	storyteller = /datum/storyteller/baotha
	traits_tier = list(TRAIT_CRACKHEAD = CLERIC_T1) //lacks a t0, i may come up with a satisfying one at some point, idk
	crafting_recipes = list(/datum/crafting_recipe/roguetown/structure/baotha_cross_stone, /datum/crafting_recipe/roguetown/structure/baotha_cross_meat)

	titles = list(
		"Lady of Heartbreak",
		"Scarlet Lady",
		"Baosumi",
		"Thorns", // Queen of thorns, Lady of thorns, etc etc.
		"Belladoth",
		"Beladoth", //SOMEONE WILL MISPELL IT, I JUST KNOW IT.
		"Leopard", // fjall
		"Solace", // i have no idea why this is here but i'll keep it along with the old names ig
	)

/datum/patron/inhumen/baotha/can_pray(mob/living/follower)
	. = ..()
	// Allows prayer in the Zzzzzzzurch(!)
	if(istype(get_area(follower), /area/rogue/under/cave/inhumen))
		return TRUE
	// Allows prayer near EEEVIL psycross
	for(var/obj/structure/fluff/psycross/cross in view(4, get_turf(follower)))
		if(cross.divine == TRUE)
			to_chat(follower, span_danger("That accursed cross interrupts my prayer."))
			return FALSE
		return TRUE
	// Allows prayers in the bath house
	if(istype(get_area(follower), /area/rogue/indoors/town/bath))
		return TRUE
	// Allows prayers if actively high on drugs.
	if(follower.has_status_effect(/datum/status_effect/buff/ozium) || follower.has_status_effect(/datum/status_effect/buff/moondust) || follower.has_status_effect(/datum/status_effect/buff/moondust_purest) || follower.has_status_effect(/datum/status_effect/buff/druqks) || follower.has_status_effect(/datum/status_effect/buff/starsugar))
		return TRUE
	// Allows prayers if the user is drunk.
	if(follower.has_status_effect(/datum/status_effect/buff/drunk))
		return TRUE
	// Allows praying atop ritual chalk of the god.
	for(var/obj/structure/ritualcircle/baotha in view(1, get_turf(follower)))
		return TRUE
	to_chat(follower, span_danger("For Baotha to hear my prayers I must either be in the church of the abandoned, near an inverted psycross, within the town's bathhouse, or actively partaking in a substance."))
	return FALSE

#define BAOTHA_SUFFERING_DIVIDER 3.535 // max bonus at 50 pain/bleedrate and pain_mod = 1

/datum/patron/inhumen/baotha/on_lesser_heal(
	mob/living/user,
	mob/living/target,
	message_out,
	message_self,
	conditional_buff,
	situational_bonus,
	is_inhumen
)
	*is_inhumen = TRUE
	*message_out = span_info("Heart-throb and loss radiate from [target].")
	*message_self = span_notice("Warm numbness soothes my suffering.")

	if(!ishuman(target))
		*message_self = span_notice("Warm numbness soothes my suffering.")
		return

	var/mob/living/carbon/human/human_target = target
	var/bonus = 0

	if(human_target.has_status_effect(/datum/status_effect/buff/druqks) \
	|| human_target.has_status_effect(/datum/status_effect/buff/drunk))
		bonus += 0.5

	if(human_target.get_stress_event(/datum/stressevent/lasthigh))
		bonus += 0.5

	if(!HAS_TRAIT(target, TRAIT_NOPAIN) || HAS_TRAIT(target, TRAIT_CRACKHEAD))
		var/raw_suffering = 0

		for(var/datum/wound/wound in human_target.get_wounds())
			raw_suffering += wound.woundpain + wound.bleed_rate

		var/suffering = sqrt(raw_suffering) / BAOTHA_SUFFERING_DIVIDER
		var/to_add = HAS_TRAIT(target, TRAIT_DEPRAVED) ? suffering : suffering * human_target.physiology.pain_mod
		bonus += min(to_add, 2)

	*conditional_buff = TRUE
	*situational_bonus = bonus

#undef BAOTHA_SUFFERING_DIVIDER
