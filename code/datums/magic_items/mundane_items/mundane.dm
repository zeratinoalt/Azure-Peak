//T1 Enchantments
/datum/magic_item/mundane/woodcut
	name = "woodcutting"
	description = "It is firm like a tree."
	glow_color = "#6B8E23"
	var/last_used

/datum/magic_item/mundane/woodcut/on_hit_structure(obj/item/i, obj/target, mob/living/user)
	if(istype(target, /obj/structure/flora))
		var/obj/structure/flora/tree = target
		tree.obj_integrity -= 70
	. = ..()

/datum/magic_item/mundane/mining
	name = "mining"
	description = "It is coated with rock."
	glow_color = "#708090"
	var/active_item = FALSE
	var/max_skill = FALSE

/datum/magic_item/mundane/mining/on_hit_structure(obj/item/i, obj/target, mob/living/user)
	if(istype(target, /obj/item/natural/rock))
		var/obj/item/natural/rock/rocktarget = target
		rocktarget.obj_integrity -= 500
	. = ..()

/datum/magic_item/mundane/mining/on_equip(obj/item/i, mob/living/user, slot)
	. = ..()
	if(active_item)
		return
	if(slot == ITEM_SLOT_HANDS)
		user.change_stat(STATKEY_WIL, 1)
		if(user.get_skill_level(/datum/skill/labor/mining)== 6)
			max_skill = TRUE
		else
			user.adjust_skillrank(/datum/skill/labor/mining, 1, TRUE)
		to_chat(user, span_notice("I feel ready to mine!"))
		active_item = TRUE
	else
		return

/datum/magic_item/mundane/mining/on_drop(obj/item/i, mob/living/user)
	. = ..()
	if(active_item)
		active_item = FALSE
		if (!max_skill)
			user.adjust_skillrank(/datum/skill/labor/mining, -1, TRUE)
		user.change_stat(STATKEY_WIL, -1)
		to_chat(user, span_notice("I feel mundane once more"))

/datum/magic_item/mundane/xylix
	name = "Xylix's boon"
	description = "It almost seems to give off the faint sound of laughter."
	glow_color = "#DAA520"
	var/active_item = FALSE

/datum/magic_item/mundane/xylix/on_equip(obj/item/i, mob/living/user, slot)
	. = ..()
	if(slot == ITEM_SLOT_HANDS)
		return
	if(active_item)
		return
	else
		user.change_stat(STATKEY_LCK, 1, "xylix_enchant")
		to_chat(user, span_notice("I feel rather lucky"))
		active_item = TRUE

/datum/magic_item/mundane/xylix/on_drop(obj/item/i, mob/living/user)
	if(active_item)
		active_item = FALSE
		user.change_stat(STATKEY_LCK, 0, "xylix_enchant")
		to_chat(user, span_notice("I feel mundane once more"))

/datum/magic_item/mundane/revealinglight
	name = "revealing light"
	description = "It emits a shining light. (Use right click to light it or dim it)"
	glow_color = "#FFB347"
	var/active = FALSE

/datum/magic_item/mundane/revealinglight/attack_right(obj/item/i, mob/living/user)
	if(!active)
		active = TRUE
		to_chat(user, span_notice("I grip [i] lightly, and it abruptly lights up with shining light"))
		i.light_system = MOVABLE_LIGHT
		if(!i.GetComponent(/datum/component/overlay_lighting))
			i.AddComponent(/datum/component/overlay_lighting)
		i.set_light_range(10)
		i.set_light_power(1)
		i.set_light_color(LIGHT_COLOR_WHITE)
		i.set_light_on(TRUE)
		i.update_icon()
	else
		active = FALSE
		to_chat(user, span_notice("I grip [i] lightly, and the light fades away"))
		i.set_light_on(FALSE)
		i.update_icon()
	. = ..()

/datum/magic_item/mundane/fairseeming
	name = "fair seeming"
	description = "It never seems to gather dirt. (Right click on it to activate the cleaning effect.)"
	glow_color = "#E6C9F0"

/datum/magic_item/mundane/fairseeming/attack_right(obj/item/i, mob/living/user)
	to_chat(user, span_notice("I grip [i] lightly, and a faint shimmer of glamour gathers around me..."))
	if(do_after(user, 2 SECONDS, target = user))
		new /obj/effect/temp_visual/cleaning_pulse(get_turf(user))
		wash_atom(user, CLEAN_STRONG)
		user.remove_stress(/datum/stressevent/sewertouched)
		to_chat(user, span_notice("The glamour settles, and I am spotless once more."))
	. = ..()

/datum/magic_item/mundane/holding
	name = "storage"
	description = "It seems bigger on the inside."
	glow_color = "#9370DB"

/datum/magic_item/mundane/holding/on_apply(obj/item/i)
	.=..()
	var/obj/item/storage = i
	var/datum/component/storage/STR = storage.GetComponent(/datum/component/storage)
	if(STR.max_w_class == WEIGHT_CLASS_SMALL)
		STR.max_w_class++
	STR.screen_max_columns = STR.screen_max_columns + 2

/datum/magic_item/mundane/magnifiedlight
	name = "magnified light"
	description = "Its light is painfully bright."
	glow_color = "#FFB347"

/datum/magic_item/mundane/magnifiedlight/on_apply(obj/item/i)
	. = ..()
	var/new_range = max(i.light_outer_range * 2, i.light_outer_range + 3)
	var/new_inner = new_range / 4
	if(i.light_system == MOVABLE_LIGHT)
		i.light_inner_range = new_inner
		i.light_outer_range = new_range
		SEND_SIGNAL(i, COMSIG_ATOM_SET_LIGHT_RANGE, new_inner, new_range)
	else
		i.set_light(new_range)
	i.update_icon()
