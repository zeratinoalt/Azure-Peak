/datum/component/holster
	/// Weapon path and its children that are allowed
	var/obj/item/rogueweapon/valid_blade
	/// Specific weapons that are allowed. Bypasses valid_blade
	var/list/obj/item/rogueweapon/valid_blades
	/// Specific weapons that are not allowed. Bypassed valid_blade
	var/list/obj/item/rogueweapon/invalid_blades

	/// Stores weapon
	var/obj/item/rogueweapon/sheathed

	var/sheathe_time = 0.1 SECONDS
	var/sheathe_sound = 'sound/foley/equip/scabbard_holster.ogg'
	var/use_icons = TRUE


/datum/component/holster/Destroy()
	if(istype(parent, /obj/item/rogueweapon/scabbard))
		var/obj/item/rogueweapon/scabbard/S = parent
		S.hol_comp = null
	if(sheathed)
		QDEL_NULL(sheathed)
	return ..()

/datum/component/holster/Initialize(obj/item/rogueweapon/arg_validblade, list/arg_valid_blades, list/arg_invalid_blades, arg_sheathe_time)
	if(!isitem(parent))
		return COMPONENT_INCOMPATIBLE

	if(arg_validblade)
		valid_blade = arg_validblade
	if(islist(arg_valid_blades) && length(arg_valid_blades))
		valid_blades = arg_valid_blades.Copy()
	if(islist(arg_invalid_blades) && length(arg_invalid_blades))
		invalid_blades = arg_invalid_blades.Copy()
	if(arg_sheathe_time)
		sheathe_time = arg_sheathe_time

	RegisterSignal(parent, COMSIG_ITEM_ATTACK_TURF, PROC_REF(search_turf))
	RegisterSignal(parent, COMSIG_ATOM_ATTACK_HAND, PROC_REF(hand_check))
	RegisterSignal(parent, COMSIG_ATOM_ATTACK_RIGHT, PROC_REF(right_click))
	RegisterSignal(parent, COMSIG_PARENT_ATTACKBY, PROC_REF(attack_by))
	RegisterSignal(parent, COMSIG_PARENT_EXAMINE, PROC_REF(examine_check))
	RegisterSignal(parent, COMSIG_ATOM_UPDATE_ICON, PROC_REF(update_icon))
	RegisterSignal(parent, COMSIG_ATOM_EXITED, PROC_REF(update_sheathed_ref))

// through some manner of unforseen tomfoolery (or admin intervention), our sheathed blade has left our contents without clearing our sheathed ref
// let's fix that and update the icon so that people don't try to draw a sword that isn't there, yes?
/datum/component/holster/proc/update_sheathed_ref(datum/source, atom/movable/thing, newloc)
	if(!sheathed || (thing != sheathed))
		return
	sheathed = null
	var/obj/item/sheathe = parent
	var/mob/living/player = (isliving(sheathe.loc) ? sheathe.loc : null)
	update_icon(player)

/datum/component/holster/proc/search_turf(atom/source, turf/T, mob/living/user)
	to_chat(user, span_notice("I search for my sword..."))
	for(var/obj/item/rogueweapon/sword/sword in T.contents)
		if(eat_sword(user, sword))
			break

/datum/component/holster/proc/weapon_check(mob/living/user, obj/A)
	if(sheathed)
		to_chat(user, span_warning("The sheath is occupied!"))
		return FALSE
	if(!istype(A, /obj/item/rogueweapon))
		return FALSE
	var/obj/item/rogueweapon/RW = A
	if(!RW.sheathe_icon)
		to_chat(user, span_warning("[A] won't fit in there."))
		return FALSE
	if(valid_blade && !istype(A, valid_blade))
		to_chat(user, span_warning("[A] won't fit in there."))
		return FALSE
	if(valid_blades)
		if(!(A.type in valid_blades))
			to_chat(user, span_warning("[A] won't fit in there."))
			return FALSE
	if(invalid_blades)
		if(A.type in invalid_blades)
			to_chat(user, span_warning("[A] won't fit in there."))
			return FALSE
	return TRUE

/datum/component/holster/proc/eat_sword(mob/living/user, obj/A)
	if(!weapon_check(user, A))
		return FALSE
	var/obj/item/I = parent
	if(I.obj_broken)
		user.visible_message(
			span_warning("[user] begins to force [A] into [parent]!"),
			span_warningbig("I begin to force [A] into [parent].")
		)
		if(!move_after(user, 2 SECONDS, target = user))
			return FALSE
		return FALSE
	if(!move_after(user, sheathe_time, target = user))
		return FALSE
	I.clear_grip_state()

	if(user.offered_item_ref?.resolve() == A)
		user.cancel_offering_item((user.m_intent == MOVE_INTENT_SNEAK))

	A.forceMove(parent)
	sheathed = A
	update_icon(user)

	user.visible_message(
		span_notice("[user] sheathes [A] into [parent]."),
		span_notice("I sheathe [A] into [parent].")
	)

	playsound(parent, sheathe_sound, 100, TRUE)
	return TRUE


/datum/component/holster/proc/Entered()
	return

/datum/component/holster/proc/puke_sword(mob/living/user)
	if(!sheathed)
		return FALSE
	if(sheathed.loc != parent) // could happen in certain niche scenarios like offering items n sheathing at the same time. should be fixed but in case there are more
		sheathed = null
		update_icon(user)
		return FALSE
	var/obj/item/I = parent
	if(I.obj_broken)
		user.visible_message(
			span_warning("[user] begins to force [sheathed] out of [I]!"),
			span_warningbig("I begin to force [sheathed] out of [I].")
		)
		if(!move_after(user, 2 SECONDS, target = user))
			return FALSE
	if(!move_after(user, sheathe_time, target = user))
		return FALSE

	// store the reference somewhere in case sheathed gets nulled.
	var/obj/item/rogueweapon/drawn = sheathed
	drawn.pickup(user)
	user.put_in_hands(drawn)
	sheathed = null
	update_icon(user)

	user.visible_message(
		span_warning("[user] draws out of [parent]!"),
		span_notice("I draw out of [parent].")
	)
	return TRUE


/datum/component/holster/proc/hand_check(datum/source, mob/user)
	var/obj/item/I = parent
	var/is_in_slot = TRUE
	if(ishuman(user))
		var/mob/living/carbon/human/human = user
		is_in_slot = (I in (list(human.backl, human.backr, human.beltl, human.beltr) + human.get_inactive_held_item()))
	if(sheathed && is_in_slot)
		puke_sword(user)
		return COMPONENT_NO_ATTACK_HAND

/datum/component/holster/proc/right_click(atom/source, mob/user)
	if(sheathed)
		puke_sword(user)

/datum/component/holster/proc/attack_by(atom/source, obj/item/I, mob/user, params)
	if(istype(I, /obj/item/needle) || istype(I, /obj/item/rogueweapon/hammer))
		return
	if(!sheathed)
		if(!eat_sword(user, I))
			return
	return COMPONENT_NO_AFTERATTACK


/datum/component/holster/proc/examine_check(datum/source, mob/user, list/examine_list)
	if(sheathed)
		examine_list += span_notice("The sheath is occupied by [sheathed]. Left-click to pull it out.")


/datum/component/holster/proc/update_icon(atom/source, mob/living/user)
	var/obj/item/I = parent
	if(use_icons)
		if(sheathed)
			I.icon_state = "[initial(I.icon_state)]_[sheathed.sheathe_icon]"
		else
			I.icon_state = "[initial(I.icon_state)]"

		I.update_slot_icon()

	I.getonmobprop(tag)

/datum/component/holster/gwstrap
	use_icons = FALSE

/datum/component/holster/gwstrap/weapon_check(mob/living/user, obj/item/A)
	if(sheathed)
		return FALSE

	if(istype(A, /obj/item/rogueweapon))
		if(A.w_class >= WEIGHT_CLASS_BULKY)
			return TRUE

	if(!istype(A, /obj/item/clothing/neck/roguetown/psicross)) //snowflake that bypasses the valid_blades that i made. i will commit seppuku eventually
		return FALSE

/datum/component/holster/gwstrap/update_icon(mob/living/user)
	var/obj/item/I = parent
	if(sheathed)
		I.worn_x_dimension = 64
		I.worn_y_dimension = 64
		I.icon = sheathed.icon
		I.icon_state = sheathed.icon_state
		I.experimental_onback = TRUE
	else
		I.icon = initial(I.icon)
		I.icon_state = initial(I.icon_state)
		I.worn_x_dimension = initial(I.worn_x_dimension)
		I.worn_y_dimension = initial(I.worn_y_dimension)
		I.experimental_onback = FALSE

	if(user)
		user.update_inv_back()

	I.getonmobprop(tag)

/datum/component/holster/simplestrap/update_icon(mob/living/user)
	var/obj/item/I = parent
	if(sheathed)
		if(sheathed.bigboy)
			I.bigboy = TRUE
		I.icon = sheathed.icon
		I.icon_state = sheathed.icon_state
		I.experimental_onback = TRUE
		I.experimental_onhip = TRUE
	else
		I.icon = initial(I.icon)
		I.icon_state = initial(I.icon_state)
		I.experimental_onback = FALSE
		I.experimental_onhip = FALSE
		I.bigboy = FALSE
	if(user)
		user.update_inv_hands()
		user.update_inv_back()
		user.update_inv_belt()

	I.getonmobprop(tag)

/datum/component/holster/handstaff/puke_sword(mob/living/user)
	. = ..()

/datum/component/holster/handstaff/eat_sword(mob/living/user, obj/A)
	. = ..()
